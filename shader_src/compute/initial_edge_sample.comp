#version 430
struct SamplePoint
{
    vec4 pos;
};

layout(local_size_x = 1, local_size_y = 1, local_size_z = 1) in;
layout(binding = 0) restrict readonly buffer sample_input
{
    SamplePoint input_sample[];
};
layout(binding = 1) restrict writeonly buffer sample_output
{
    SamplePoint output_sample[];
};

uniform int work_group_offset;
uniform int max_sample_points;
uniform float sample_length;

vec4 read(highp uint index, highp uint offset)
{
    return input_sample[index + offset].pos;
}

void write(highp uint index, highp uint offset, vec4 pos)
{
    output_sample[index + offset].pos = pos;
}

void main() {
    highp uint offset = (gl_WorkGroupID.x + work_group_offset) * max_sample_points;
    highp uint current_read_index = 0;
    highp uint current_write_index = 1;

    vec4 first_point = read(0, offset);
    vec4 last_point = read(1, offset);
    vec3 last_written_point = first_point.xyz;

    float edge_length = distance(first_point.xyz, last_point.xyz);
    float samples = float(ceil(edge_length/sample_length));
    float sample_distance = edge_length/samples;

    // initialize variables used over the next loop
    vec3 next_point = last_point.xyz;
    float distance_to_next = distance(next_point, last_written_point);

    while (distance_to_next >= sample_distance * 0.99) {
        float t = sample_distance / distance_to_next;
        t = clamp(t, 0.0, 1.0);
        last_written_point = (last_written_point * (1.0 - t) + next_point * t);
        write(current_write_index++, offset, vec4(last_written_point.xyz, 1.0));
        distance_to_next = distance(next_point, last_written_point);
    }

    // always store last edge point and mark it as last with w-value -1
    write(current_write_index++, offset, vec4(next_point.xyz, -1.0));

    // store the first point with sum of all points as w value
    write(0, offset, vec4(first_point.xyz, int(current_write_index)));

    while (current_write_index <= first_point.w) {
        write(current_write_index++, offset, vec4(0.0, 0.0, 0.0, 0.0));
    }
}